#> Kick people without the proper gameID out (also acts on firstjoin players) + crash detection
scoreboard players add $curr leavecheck 1
scoreboard players add @a leavecheck 1
execute as @a[scores={leftgame=1..}] run function lobby:joinreset
execute as @a unless score @s leavecheck = $curr leavecheck run function lobby:joinreset
execute as @a[team=!Lobby,team=!Spectator] unless score @s gameID = $current gameID run function lobby:joinreset

#> Create a gameID if it somehow doesn't exist anymore
execute unless score $current gameID matches 0..2147483646 run function lobby:settings/gameidcycle

#> Joinpads
scoreboard players enable @a joinred
scoreboard players enable @a joingreen
scoreboard players enable @a joinspec

execute if score $gamestate CmdData matches 0..3 run function lobby:joinpads
execute unless score $gamestate CmdData matches 0..3 run function lobby:joinpads_off

tag @a[team=!Spectator,scores={joinspec=1..}] add WarpSpectate
execute as @a[team=Spectator,scores={joinspec=1..}] run tellraw @s [{text:"[",color:"dark_gray"},{text:"!",color:"red",bold:true},{text:"] ",color:"dark_gray"},{translate:"error.already_spectating",color:"red"}]
execute as @a[team=Spectator,scores={joinspec=1..}] at @s run playsound joinfail master @s ~ ~ ~ 1 0

scoreboard players reset @a[scores={joinred=1..}] joinred
scoreboard players reset @a[scores={joingreen=1..}] joingreen
scoreboard players reset @a[scores={joinspec=1..}] joinspec

#> Teleporters
function lobby:poles/main

#> Cancel match
execute if score $gamestate CmdData matches 0.. run scoreboard players enable @a[team=Lobby] cancelMatch
execute as @a[team=!Lobby,team=!Developer] run trigger cancelMatch set 0
execute as @a[team=Lobby,scores={cancelMatch=1..}] run function lobby:settings/cancel/interact

#> Readyup
execute if score $gamestate CmdData matches 0 run function lobby:readyup
execute if score $gamestate CmdData matches 1 if score $precountdown CmdData matches ..19 run function lobby:readyup
execute if score $gamestate CmdData matches 1 unless score $precountdown CmdData matches ..19 as @a run trigger readyup set 0
execute unless score $gamestate CmdData matches 0..1 run bossbar set bar_ready_r players
execute unless score $gamestate CmdData matches 0..1 run bossbar set bar_ready_g players
execute unless score $gamestate CmdData matches 0..1 run bossbar set bar_lobby_hearts name [{text:"\uE01A\uDAFF\uDFFE",color:"#a8a020",shadow_color:[0,0,0,0]},{text:"\uE01A\uDAFF\uDFFE",color:"#a8a020",shadow_color:[0,0,0,0]},{text:"\uE01A\uDAFF\uDFFE",color:"#a8a020",shadow_color:[0,0,0,0]},{text:"\uE01A\uDAFF\uDFFE",color:"#a8a020",shadow_color:[0,0,0,0]},{text:"\uE01A\uDAFF\uDFFE",color:"#a8a020",shadow_color:[0,0,0,0]},{text:"\uE01A\uDAFF\uDFFE",color:"#a8a020",shadow_color:[0,0,0,0]},{text:"\uE01A\uDAFF\uDFFE",color:"#a8a020",shadow_color:[0,0,0,0]},{text:"\uE01A\uDAFF\uDFFE",color:"#a8a020",shadow_color:[0,0,0,0]},{text:"\uE01A\uDAFF\uDFFE",color:"#a8a020",shadow_color:[0,0,0,0]},{text:"\uE01A\uDAFF\uDFFE",color:"#a8a020",shadow_color:[0,0,0,0]}]

#> Modification room
execute if score $gamestate CmdData matches -1 run function lobby:settings/loop

#> Bossbars
function bossbars:loop

#> Hide bars if games are inactive
execute unless score $gamestate CmdData matches 2.. run bossbar set bar_prep players
execute unless score $gamestate CmdData matches 2.. run bossbar set bar_g_main players
execute unless score $gamestate CmdData matches 2.. run bossbar set bar_g_green players
execute unless score $gamestate CmdData matches 2.. run bossbar set bar_g_red players
execute unless score $gamestate CmdData matches 2.. run bossbar set bar_r_main players
execute unless score $gamestate CmdData matches 2.. run bossbar set bar_r_red players
execute unless score $gamestate CmdData matches 2.. run bossbar set bar_r_green players

#> Parkour
execute as @a[team=Lobby] run function lobby:parkour/main
effect clear @a[team=!Lobby,tag=inParkour] invisibility
tag @a[team=!Lobby,tag=inParkour] remove inParkour
tag @a[team=!Lobby,tag=resettimeonce] remove resettimeonce

#> Reset stats/tags
scoreboard players reset @a[team=Lobby] MVPkill
scoreboard players reset @a[team=Lobby] MVPdeath
scoreboard players reset @a[team=Lobby] MVProcket
scoreboard players reset @a[team=Lobby] MVPdamage

#> Translator credits
execute as @e[type=item_display,tag=Globe,limit=1] run function lobby:credits/translators/loop
execute as @e[type=item_display,tag=TranslatorCredit,tag=Credit] run function lobby:credits/translators/loop
execute as @e[type=text_display,tag=TranslatorCredit,tag=Credit] run function lobby:credits/translators/loop
